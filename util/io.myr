use std

use "defines"

pkg utilfcgi =
	const sendrecord	: (fd : std.fd, header : byte[:], content : byte[:] -> bool)
	const readheader	: (fd : std.fd -> std.option((byte, rtype, uint16, std.size, std.size)))
;;

const sendrecord = {fd, header, content
	match std.writeall(fd, header)
	| `std.Ok sz:
		if sz < header.len
			goto err
		;;
	| `std.Err _: goto err
	;;

	match std.writeall(fd, content)
	| `std.Ok sz:
		if sz < content.len
			goto err
		;;
	| `std.Err _: goto err
	;;

	-> true

:err
	-> false
}

const readheader = {fd
	var buf : byte[Headerlen]
	var version, what, id, conlen, padlen

	match std.readall(fd, buf[:])
	| `std.Ok sz:
		if sz < buf.len
			goto err
		;;
	| `std.Err _: goto err
	;;

	version		= buf[0]
	what		= (buf[1] : rtype)
	id		= std.getbe16(buf[2:4])
	conlen		= (std.getbe16(buf[4:6]) : std.size)
	padlen		= (buf[6] : std.size)
	/* reserved	= buf[7] */

	-> `std.Some (version, what, id, conlen, padlen)

:err
	-> `std.None
}
