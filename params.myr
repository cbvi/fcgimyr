use std

use "types"

pkg fcgi =
	const parse_params	: (req : request#, buf : byte[:] -> void)
;;

const getval = {buf, keylen, vallen
	var key, val

	if buf.len >= keylen
		key = buf[:keylen]
		buf = buf[keylen:]
	else
		goto err
	;;

	if buf.len >= vallen
		val = buf[:vallen]
		buf = buf[vallen:]
	else
		goto err
	;;

	-> `std.Some ((key, val), buf)

:err
	-> `std.None
}

const getlen = {buf
	var lens : uint32[2]

	for var i = 0; i < 2; i++
		if buf.len <= 1
			goto err
		elif buf[0] >> 7 == 0
			lens[i] = (buf[0] : uint32)
			buf = buf[1:]
		else
			if buf.len <= 4
				goto err
			;;
			lens[i] = (((buf[0] : uint32) & 0x7f) << 24) + \
				((buf[1] : uint32) << 16) + \
				((buf[2] : uint32) << 8) + \
				((buf[3] : uint32))
			buf = buf[4:]
		;;
	;;

	-> `std.Some ((lens[0], lens[1]), buf)

:err
	-> `std.None
}

const parse_params = {req, buf
	var keylen, vallen

	while true
		match getlen(buf)
		| `std.Some (lens, b):
			(keylen, vallen) = lens
			buf = b
		| `std.None: break
		;;

		match getval(buf, keylen, vallen)
		| `std.Some (kv, b):
			std.slpush(&req.params, kv)
			buf = b
		| `std.None: break
		;;
	;;

}
