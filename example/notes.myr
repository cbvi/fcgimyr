use std
use sys
use fcgi

type note = struct
	title : byte[:]
	body : byte[:]
;;

var srv
var notes

const mknote = {stdin
	var s

	if !std.hasprefix(stdin, "title=")
		-> `std.None
	;;

	s = std.sldup(stdin)
	std.slpush(&notes, (notes.len, s))
	-> `std.Some s
}

const notesfree = {n
	for (i, s) : n
		std.slfree(s)
	;;
	std.slfree(n)
}

const shownote = {query
	var q : byte[:][2]
	var id

	std.bstrsplit(q[:], query, "=")

	if !std.sleq(q[0], "id")
		goto err
	;;
	
	match std.intparse(q[1])
	| `std.Some n: id = n
	| `std.None: goto err
	;;

	if notes.len <= id
		goto err
	;;

	-> `std.Some notes[id]

:err
	-> `std.None
}


const handler = {req
	var b, response
	var meth = "GET", query = ""
	var stop = false

	b = std.mksb()

	std.sbputs(b, "Content-Type: text/html\r\n\r\n")

	for kv : req.params
		match kv
		| ("REQUEST_METHOD", v): meth = v
		| ("QUERY_STRING", v): query = v
		| _:
		;;
	;;

	if query.len > 0
		match shownote(query)
		| `std.Some (id, text):
			var s = std.fmt("<p>{}</p>", text)
			std.sbputs(b, s)
			std.slfree(s)
			std.sbputs(b, "<p><a href=\"?\">&lt; back</a></p>")
		| `std.None:
			std.sbputs(b, "<p>invalid note</p>")
		;;
	else
		std.sbputs(b,
			"<form method=\"POST\">" \
			"title: <input type=\"text\" name=\"title\" style=\"width:40%;\"><br>" \
			"text: <textarea name=\"body\" rows=\"5\" style=\"width:40%;\"></textarea><br>" \
			"<input type=\"submit\" value=\"create\"><br>" \
			"</form>" \
		)
	;;

	if std.sleq(meth, "POST") && req.stdin.len > 0
		match mknote(req.stdin)
		| `std.Some n:
			var s

			s = std.fmt("created new note <i>{}</i><br>", n)
			std.sbputs(b, s)
			std.slfree(s)
		| `std.None:
		;;
	;;

	for (id, n) : notes
		var s = std.fmt("<a href=\"?id={}\">{}</a> {}<br>", id, id, n)
		std.sbputs(b, s)
		std.slfree(s)
	;;

	response = std.sbfin(b)
	fcgi.respond(req, response)

	std.slfree(response)
}

const sighandler = {
	var act, oact
	act = [
		.handler = ({
			std.put("\rexiting\n")
			fcgi.shutdown(srv)
		} : byte#)
	]
	sys.sigaction(sys.Sigint, &act, &oact)
}

const main = {args
	var opts, path, unix

	std.startalloctrace("/tmp/trace.out")

	opts = std.optparse(args, &[
		.argdesc = "path",
		.minargs = 1,
		.maxargs = 1,
	])

	path = opts.args[0]

	unix = std.fmt("unix!{}", path)
	srv = std.try(fcgi.announce(unix))
	std.slfree(unix)

	std.slfree(opts.args)

	notes = [][:]

	sighandler()

	fcgi.serve(srv, handler)

	notesfree(notes)

	std.endalloctrace()
}
